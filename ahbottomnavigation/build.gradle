plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.aurelhubert.ahbottomnavigation'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        compileSdkVersion 34
        versionCode 40
        versionName "2.1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    publishing {
        singleVariant('release')
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.12.0'
}

// No lint is setup in the project so the lint tasks generate noise for Jitpack.
tasks.configureEach { task ->
    if (task.name == 'lintVitalAnalyzeRelease') {
        task.enabled = false
        logger.lifecycle("Disabling lintVitalAnalyzeRelease task for ${project.name}.")
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.getByName('release')

                groupId = 'com.github.wix-playground'
                artifactId = 'ahbottomnavigation'
                version = '0.0.0' // Jitpack uses your Git tag
            }
        }
    }
}
